plugins {
    id 'com.android.application'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release_key {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.jayurewards.tablet"
        minSdkVersion 24
        targetSdkVersion 30

        // Change version code and name before publishing
        versionCode 10
        versionName "1.4.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            // Allow http traffic (for the local host only!)
            manifestPlaceholders = [usesCleartextTraffic: "true"]

            // Must use double quotations to enforce a string for values that start with an integer
            buildConfigField "String", 'googleSigninWebClientId', '"336479330285-rsu2kcpr34v39el6qh9oid1kli137s45.apps.googleusercontent.com"'
            buildConfigField "String", 'restApiUrl', '"https://api-dev-restricted.jayu.us"'
            buildConfigField "String", 'merchantPortalurl', '"https://portal-dev-restricted.jayu.us"'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Only allow https traffic
            manifestPlaceholders = [usesCleartextTraffic: "false"]

            // Google sign in may only work on the production build.
            buildConfigField "String", 'googleSigninWebClientId', '"754440637669-1hqpc17bljv653a591pmk1n1t2q5tche.apps.googleusercontent.com"'
            buildConfigField "String", 'restApiUrl', '"https://d7eead3c-d79c-403a-b4f6-ad2d1928184b.jayu.us"'
            buildConfigField "String", 'merchantPortalurl', '"https://portal.jayu.us"'
            signingConfig signingConfigs.release_key
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    /**
     * Android
     */
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.browser:browser:1.3.0"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    /**
     * Firebase
     */
    implementation platform('com.google.firebase:firebase-bom:28.4.2')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.android.gms:play-services-auth:19.2.0'

    /**
     * Retrofit Networking
     */
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'

    /**
     * Glide
     */
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

/**
 * Country dialing codes with flag picker
 */
    implementation 'com.hbb20:ccp:2.5.3'

    // Circular images
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // Image Cropper "uCrop"
    implementation 'com.github.yalantis:ucrop:2.2.6'

    // Blur background
    implementation 'com.eightbitlab:blurview:1.6.6'


}